FUNCTION "MTS_MapTakeParts" : Void
TITLE = Take parts from the map
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
//Move parts from specified area in the map to the destination array
   VAR_INPUT 
      II_RowNr : Int;   // Row number for first part to insert
      II_LineNr : Int;   // Line number for first part to insert
   END_VAR

   VAR_IN_OUT 
      X_Destination : Array[*, *] of "UDT_MatCard";   // Destination array of free positions
      X_Map : Array[*, *] of "UDT_MatCard";   // Map of parts
   END_VAR

   VAR_TEMP 
      TI_RowNr : Int;   // Row number
      TI_LineNr : Int;   // Line number
      TIDestRowBoundLow : Int;   // Lower limit of row dimension of destination array
      TIDestRowBoundUp : Int;   // Upper limit of row dimension of destination array
      TIDestLineBoundLow : Int;   // Lower limit of line dimension of destination array
      TIDestLineBoundUp : Int;   // Upper limit of line dimension of destination array
      TIMapRowBoundLow : Int;   // Lower limit of row dimension of map
      TIMapRowBoundUp : Int;   // Upper limit of row dimension of map
      TIMapLineBoundLow : Int;   // Lower limit of line dimension of map
      TIMapLineBoundUp : Int;   // Upper limit of line dimension of map
      TB1WrongIndex : Bool;   // Index is out of limits
      TI_IndexRow : Int;   // Row array index
      TI_IndexLine : Int;   // Line array index
   END_VAR


BEGIN
	
	REGION Copy input parameters to temporary variables
	    #TI_RowNr := #II_RowNr;
	    #TI_LineNr := #II_LineNr;
	END_REGION
	
	REGION Destination array dimensions
	    #TIDestRowBoundLow := DINT_TO_INT(LOWER_BOUND(ARR := #X_Destination, DIM := 1)); // Lower limit of row dimension
	    #TIDestRowBoundUp := DINT_TO_INT(UPPER_BOUND(ARR := #X_Destination, DIM := 1)); // Upper limit of row dimension
	    #TIDestLineBoundLow := DINT_TO_INT(LOWER_BOUND(ARR := #X_Destination, DIM := 2)); // Lower limit of line dimension
	    #TIDestLineBoundUp := DINT_TO_INT(UPPER_BOUND(ARR := #X_Destination, DIM := 2)); // Upper limit of row dimension
	END_REGION
	
	REGION Map dimensions
	    #TIMapRowBoundLow := DINT_TO_INT(LOWER_BOUND(ARR := #X_Map, DIM := 1)); // Lower limit of row dimension
	    #TIMapRowBoundUp := DINT_TO_INT(UPPER_BOUND(ARR := #X_Map, DIM := 1)); // Upper limit of row dimension
	    #TIMapLineBoundLow := DINT_TO_INT(LOWER_BOUND(ARR := #X_Map, DIM := 2)); // Lower limit of line dimension
	    #TIMapLineBoundUp := DINT_TO_INT(UPPER_BOUND(ARR := #X_Map, DIM := 2)); // Upper limit of row dimension
	    
	END_REGION
	
	REGION Evaluation OF indexes
	    // index of any position in the destination array cannot be out of dimension of the map
	    IF #TI_RowNr < #TIMapRowBoundLow
	        OR (#TI_RowNr + #TIDestRowBoundUp - #TIDestRowBoundLow) > #TIMapRowBoundUp
	        OR #TI_LineNr < #TIMapLineBoundLow
	        OR (#TI_LineNr + #TIDestLineBoundUp - #TIDestLineBoundLow) > #TIMapLineBoundUp
	    THEN
	        #TB1WrongIndex := TRUE;
	    ELSE
	        #TB1WrongIndex := FALSE;
	    END_IF;
	END_REGION
	
	REGION Take parts
	    IF NOT #TB1WrongIndex THEN
	        FOR #TI_IndexRow := #TIDestRowBoundLow TO #TIDestRowBoundUp DO
	            FOR #TI_IndexLine := #TIDestLineBoundLow TO #TIDestLineBoundUp DO
	                REGION Count map indexes
	                    #TI_RowNr := #II_RowNr + #TI_IndexRow - #TIDestRowBoundLow;
	                    #TI_LineNr := #II_LineNr + #TI_IndexLine - #TIDestLineBoundLow;
	                END_REGION
	                "MTS_Move"(XS_Source := #X_Map[#TI_RowNr, #TI_LineNr],
	                           OS_Destination => #X_Destination[#TI_IndexRow, #TI_IndexLine]);
	            END_FOR;
	        END_FOR;
	    END_IF;
	END_REGION
	
END_FUNCTION

