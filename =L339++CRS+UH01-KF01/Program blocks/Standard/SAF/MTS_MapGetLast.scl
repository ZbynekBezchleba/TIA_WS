FUNCTION "MTS_MapGetLast" : "UDT_MatCard"
TITLE = Move map forward/backward
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
//Move all material data in the map to a position in next/previous row
   VAR_INPUT 
      I_Map : Array[*, *] of "UDT_MatCard";   // Map of parts
   END_VAR

   VAR_TEMP 
      TI_IndexRow : Int;   // Row array index
      TI_IndexLine : Int;   // Line array index
      TDIMapRowBoundLow : DInt;   // Lower limit of row dimension
      TDIMapRowBoundUp : DInt;   // Upper limit of row dimension
      TDIMapLineBoundLow : DInt;   // Lower limit of line dimension
      TDIMapLineBoundUp : DInt;   // Upper limit of line dimension
      TS_Part : "UDT_MatCard";   // Last part
   END_VAR


BEGIN
	
	REGION Map dimensions (array limits)
	    #TDIMapRowBoundLow := LOWER_BOUND(ARR := #I_Map, DIM := 1);
	    #TDIMapRowBoundUp := UPPER_BOUND(ARR := #I_Map, DIM := 1);
	    #TDIMapLineBoundLow := LOWER_BOUND(ARR := #I_Map, DIM := 2);
	    #TDIMapLineBoundUp := UPPER_BOUND(ARR := #I_Map, DIM := 2);
	END_REGION
	
	REGION Finding the last part
	    FOR #TI_IndexRow := #TDIMapRowBoundUp TO #TDIMapRowBoundLow BY -1 DO
	        FOR #TI_IndexLine := #TDIMapLineBoundUp TO #TDIMapLineBoundLow BY -1 DO
	            IF #I_Map[#TI_IndexRow, #TI_IndexLine].Occupied THEN
	                #MTS_MapGetLast := #I_Map[#TI_IndexRow, #TI_IndexLine];
	                RETURN;
	            END_IF;
	        END_FOR;
	    END_FOR;
	    "MTS_Clear"(#TS_Part);
	    #MTS_MapGetLast := #TS_Part;
	END_REGION
	
END_FUNCTION

