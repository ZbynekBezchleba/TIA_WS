FUNCTION "MTS_MapInsertPart" : Void
TITLE = Insert part to the map
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
//Insert the part to specified position in the map
   VAR_INPUT 
      II_RowIndex : Int;   // Row index for insert new part
      II_LineIndex : Int;   // Line index for insert new part
      IS_Part : "UDT_MatCard";   // Data of part to insert
   END_VAR

   VAR_IN_OUT 
      X_Map : Array[*, *] of "UDT_MatCard";   // Map of parts
   END_VAR

   VAR_TEMP 
      TI_IndexRow : Int;   // Row array index
      TI_IndexLine : Int;   // Line array index
      TDIMapRowBoundLow : DInt;   // Lower limit of row array
      TDIMapRowBoundUp : DInt;   // Upper limit of row array
      TDIMapLineBoundLow : DInt;   // Lower limit of line array
      TDIMapLineBoundUp : DInt;   // Upper limit of line array
      TB1WrongIndex : Bool;   // Index is out of limits
   END_VAR


BEGIN
	
	REGION Copy input parameters to temporary variables
	    #TI_IndexRow := #II_RowIndex;
	    #TI_IndexLine := #II_LineIndex;
	END_REGION
	
	REGION Array limits
	    #TDIMapRowBoundLow := LOWER_BOUND(ARR := #X_Map, DIM := 1);
	    #TDIMapRowBoundUp := UPPER_BOUND(ARR := #X_Map, DIM := 1);
	    #TDIMapLineBoundLow := LOWER_BOUND(ARR := #X_Map, DIM := 2);
	    #TDIMapLineBoundUp := UPPER_BOUND(ARR := #X_Map, DIM := 2);
	END_REGION
	
	REGION Evaluation OF indexes
	    IF #TI_IndexRow < #TDIMapRowBoundLow
	        OR #TI_IndexRow > #TDIMapRowBoundUp
	        OR #TI_IndexLine < #TDIMapLineBoundLow
	        OR #TI_IndexLine > #TDIMapLineBoundUp
	    THEN
	        #TB1WrongIndex := TRUE;
	    ELSE
	        #TB1WrongIndex := FALSE;
	    END_IF;
	END_REGION
	
	REGION Insert part
	    IF NOT #TB1WrongIndex THEN
	        #X_Map[#TI_IndexRow, #TI_IndexLine] := #IS_Part;
	    END_IF;
	END_REGION
END_FUNCTION

